plugins {
	id 'jacoco'
	id "org.jetbrains.kotlin.jvm" version "1.3.20"
	id "org.jetbrains.kotlin.plugin.spring" version "1.3.20"
	id "org.springframework.boot" version "2.1.1.RELEASE"
	id "io.spring.dependency-management" version "1.0.6.RELEASE"
	id("io.gitlab.arturbosch.detekt") version "1.0.0-RC12"
}

group = 'no.sonat.kotlindemo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

repositories {
	jcenter()
}

processResources {
	filesMatching('application.properties') {
		expand(project.properties)
	}
}

test {
	useJUnitPlatform()
	testLogging {
		exceptionFormat "full"
		events "started", "skipped", "passed", "failed"
		showStandardStreams false
		showStackTraces true
	}

	jvmArgs = [
		"-Duser.timezone=Europe/Oslo"
	]
}

test.finalizedBy(jacocoTestReport)

jacoco {
	toolVersion = "0.8.2"
}

jacocoTestReport {
	reports {
		html.enabled true
		xml.enabled true
		csv.enabled false
	}
}

detekt {
	toolVersion = "1.0.0-RC12"
	input = files("src/main/kotlin")
	filters = ".*/resources/.*,.*/build/.*"
}

bootJar {
	archiveName = "kotlin-demo.jar"
}

def coroutinesVersion = "1.1.1"
def elasticVersion = "6.6.0"

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.boot:spring-boot-starter-webflux')
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
	implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
	implementation("org.jetbrains.kotlin:kotlin-reflect")
	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion")
	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$coroutinesVersion")
	implementation("net.logstash.logback:logstash-logback-encoder:5.2")
	implementation("ch.qos.logback:logback-access")
	implementation("io.springfox:springfox-swagger2:2.9.2")
	implementation("io.springfox:springfox-swagger-ui:2.9.2")

	implementation('software.amazon.awssdk:s3:2.4.4')

	implementation "org.elasticsearch:elasticsearch:$elasticVersion"
	implementation "org.elasticsearch.client:elasticsearch-rest-client:$elasticVersion"
	implementation "org.elasticsearch.client:elasticsearch-rest-high-level-client:$elasticVersion"
	testImplementation('pl.allegro.tech:embedded-elasticsearch:2.8.0')

	runtimeOnly('org.springframework.boot:spring-boot-devtools')
	testImplementation "com.github.tomakehurst:wiremock:2.20.0"
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude module: 'junit'
	}
	testImplementation('org.junit.jupiter:junit-jupiter-api')
	testImplementation('com.nhaarman:mockito-kotlin:1.6.0')
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
}
